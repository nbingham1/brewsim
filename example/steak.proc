gas >= 1 and person >= 1 ->
coldOven -= 1,
hotOven += 1,
seconds += 5*60
: "preheat oven to 350"

1 ->
person -= 1;
garlicCloves -= 15,
bakingSheet -= 1,
seconds += 3,
bakingSheetWithGarlic += 1
: "put garlic on baking sheet"

1 ->
person -= 1;
bakingSheetWithGarlic -= 1,
tbspOliveOil -= 1,
seconds += 2,
bakingSheetWithOiledGarlic += 1
: "drizzle olive oil"

person >= 1 and hotOven >= 1 ->
seconds += 20*60,
bakingSheetWithOiledGarlic -= 1,
hotBakingSheetWithCookedGarlic += 1
: "bake garlic"

person >= 1 ->
seconds += 5*60,
hotBakingSheetWithCookedGarlic -= 1,
bakingSheetWithCookedGarlic += 1
: "cool garlic"

foodProcessor >= 1
-> person -= 1;
tbspOliveOil -= 2,
bakingSheetWithCookedGarlic -= 1,
garlicPaste += 4,
bakingSheet += 1,
seconds += 10
: "put garlic in food processor"

1 ->
salt -= 1,
pepper -= 1,
person -= 1 ;
steak -= 1,
garlicPaste -= 1,
seasonedSteak += 1
: "season steak"

pan >= 1 and person >= 1 ->
seasonedSteak -= 1,
seconds += 10*60,
cookedSteak += 1
: "cook steak"

have 1 gas,
	1 person,
	1 salt,
	1 pepper,
	4 steak,
	15 garlicCloves,
	1 bakingSheet,
	1 coldOven,
	4 tbspOliveOil,
	1 foodProcessor,
	1 pan;
need cookedSteak >= 4;
keep steak >= 0 and
	garlicCloves >= 0 and
	bakingSheet >= 0 and
	coldOven >= 0 and
	tbspOliveOil >= 0 and
	foodProcessor >= 0 and
	pan >= 0 and
	seasonedSteak >= 0 and
	garlicPaste >= 0 and
	bakingSheetWithGarlic >= 0 and
	bakingSheetWithOiledGarlic >= 0 and
	bakingSheetWithCookedGarlic >= 0 and
	hotBakingSheetWithCookedGarlic >= 0;
let duration = diff(seconds);
min duration;
